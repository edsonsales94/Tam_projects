#INCLUDE "MATR875.CH"
#INCLUDE "PROTHEUS.CH"


/*/{Protheus.doc} PesqLote

Este relatório demonstra a situação de cada item da estrutura do produto Pai em relaçãoo ao seu saldo , seu empenho , suas entradas previstas e sua classe ABC.

@type function
@author Rodrigo de A. Sartorio
@since 02.08.95

/*/

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MATR875   ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 02.08.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relacao da Analise de Estoques Referentes ao Produto Pai    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marcelo Pim.³24/11/97³13529A³ Criacao de param.p/ listar itens fantasm.³±±
±±³            ³        ³13529A³ e listar estru. do fantasma.             ³±±
±±³Edson   M.  ³19/01/98³XXXXXX³ Inclusao do Campo C2_SLDOP.              ³±±
±±³Edson   M.  ³03/02/98³XXXXXX³ Subst. do Campo C2_SLDOP por funcao.     ³±±
±±³Rodrigo Sart³20/05/98³14386A³ Acerto no filtro das SC's (SC1)          ³±±
±±³Marcos Simi.³01/07/98³13110A³ Acertos no lay-out de impressao.         ³±±
±±³Rodrigo Sart³04/11/98³XXXXXX³ Acerto p/ Bug Ano 2000                   ³±±
±±³Fernando J. ³07/02/99³META  ³Imprimir OP's Firmes, Previstas ou Ambas. ³±±
±±³Patricia Sal³27/07/00³005280³Incluir mv_par06: "Qual Almoxarifado ?"   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function RANLESTR()
LOCAL titulo 	:= STR0001	//"Relacao para Analise dos Estoques"
LOCAL cString  	:= "SG1"
LOCAL wnrel 	:= "MATR875"
LOCAL cDesc1   	:= STR0002	//"Este relat¢rio demonstra a situa‡„o de cada item da estrutura"
LOCAL cDesc2   	:= STR0003	//"do produto Pai em rela‡„o ao seu saldo , seu empenho , suas  "
LOCAL cDesc3   	:= STR0004	//"entradas previstas e sua classe ABC."
LOCAL Tamanho  	:= "G"
LOCAL i			:= 0

PRIVATE aReturn	:= {STR0005,1,STR0006, 1, 2, 1, "",1 }	//"Zebrado"###"Administracao"
PRIVATE nLastKey:= 0
PRIVATE cPerg 	:= "MTR875"  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Funcao utilizada para verificar a ultima versao dos fontes      ³
//³ SIGACUS.PRW, SIGACUSA.PRX e SIGACUSB.PRX, aplicados no rpo do   |
//| cliente, assim verificando a necessidade de uma atualizacao     |
//| nestes fontes. NAO REMOVER !!!							        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF !(FindFunction("SIGACUS_V") .and. SIGACUS_V() >= 20050512)
    Final("Atualizar SIGACUS.PRW !!!")
Endif
IF !(FindFunction("SIGACUSA_V") .and. SIGACUSA_V() >= 20050512)
    Final("Atualizar SIGACUSA.PRX !!!")
Endif
IF !(FindFunction("SIGACUSB_V") .and. SIGACUSB_V() >= 20050512)
    Final("Atualizar SIGACUSB.PRX !!!")
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01     // De Produto                                   ³
//³ mv_par02     // Ate Produto                                  ³
//³ mv_par03     // Lista itens fantasmas   (1-Sim 2-Nao)        ³
//³ mv_par04     // Lista Estr. do fantasma (1-Sim 2-Nao)        ³
//³ mv_par05     // Impr. OP's Firmes, Previstas ou Ambas        ³
//³ mv_par06     // De   Almoxarifado ?                          ³ 
//³ mv_par07     // Ate  Almoxarifado ?                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel:=SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,"",.F.,Tamanho)

If nLastKey == 27
	Set Filter To
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	Set Filter To
	Return
Endif

RptStatus({|lEnd| R875Imp(@lEnd,wnRel,titulo,tamanho,cString)},titulo)

Return NIL
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R875Imp  ³ Autor ³ Waldemiro L. Lustosa  ³ Data ³ 13.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR875			                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R875Imp(lEnd,wnRel,titulo,tamanho,cString)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL cRodaTxt := STR0007	//"PRODUTO(S)"
Local i			:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para controle do cursor de progressao do relatorio ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL nSaldoEst:= 0
LOCAL aSalAlm  := {}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Private padrao de todos os relatorios         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE nomeprog := "MATR875"
PRIVATE nTipo    := 0
PRIVATE cabec1,cabec2
PRIVATE nCntImpr := 0
PRIVATE _cGrupoAnt:=""//Honda Lock
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de linha e pagina                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE li := 80 ,m_pag := 1
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Array usado na impressao da estrutura                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aEstru:={}
PRIVATE aPai  :={}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis usadas na impressao dos componentes da Estrutura   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE nSaldoEst:=nEmp:=nSCPro:=nPCPro:=nOPPro:=nPVPro:=nSaldo99:=nSaldoL:=0
PRIVATE dUsai := CtoD("  /  /  ")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis usadas na impressao da totalizacao do produto PAI  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE nTotSaldo:=nTotEmp:=nTotSCPro:=nTotPCPro:=nTotOPPro:=nTotPVPro:=0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta os Cabecalhos                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cabec1 := "CODIGO          DESCRICAO                      TP GRUP UM   SALDO TOTAL    EMPENHO PARA          SALDO          SALDO           OP's           PV's     PONTO DE         LOTE   PRAZO EST.EM     NRO. LOTE     DATA   CL  TP"
cabec2 := "                                                             EM ESTOQUE  ORDEM PRODUCAO    EM PROCESSO       NA LINHA      COLOCADAS      COLOCADOS       PEDIDO    ECONOMICO ENTREGA  MESES                 ENTREGA     N/F"
*****  	   123456789012345 123456789012345678901234567890 12 1234 12 999,999,999.99 999,999,999.99 999,999,999.99 999,999,999.99 999,999,999.99 999,999,999.99 9,999,999.99 9,999,999.99 99999 D  99999 99,999,999.99  11/11/1111 A  X
***** 	   0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21
***** 	   01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

dbSelectArea("SG1")
dbSetOrder(1)
dbSeek(xFilial())

SetRegua(LastRec())

lFirst	 :=.F.
lContinua := .T.

While !Eof() .And. xFilial()==SG1->G1_FILIAL
	
	If lEnd
		@ PROW()+1,001 PSAY STR0010	//"CANCELADO PELO OPERADOR"
		lContinua := .F.
		Exit
	EndIf
	
	IncRegua()
	
	If G1_COD < mv_par01 .Or. G1_COD > mv_par02
		dbSkip()
		Loop
	EndIf
	
	If mv_par04 == 2
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona SB1 para produto Pai	     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SB1")
		dbSetOrder(1)
		dbSeek(xFilial()+SG1->G1_COD)
		If B1_FANTASM == "S"
			dbSelectArea("SG1")
			dbSkip()
			Loop
		Endif
	Endif
	dbSelectArea("SG1")
	
	nScan:=aScan(aPai,G1_COD)
	If nScan == 0
		AADD(aPai,G1_COD)
	Else
		dbSkip()
		Loop
	EndIf
	
	aEstru:={}
	nReg:=Recno()
	Explode(G1_COD)
	dbGoto(nReg)
	
	If li > 58
		Cabec(titulo,cabec1,cabec2,nomeprog,Tamanho,nTipo)
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Adiciona 1 ao contador de registros impressos         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nCntImpr++
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Zera variaveis para Impressao do Total do produto Pai		 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nSaldoEst:=nTotSaldo:=nTotEmp:=nTotSCPro:=nTotPCPro:=nTotOPPro:=nTotPVPro:=0      
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona SB1 para produto Pai					             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB1")
	dbSetOrder(1)
	dbSeek(xFilial()+SG1->G1_COD)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona SB2 para Produto Pai					             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB2")
	dbSetOrder(1)
	dbSeek(xFilial()+SG1->G1_COD)
	While B2_FILIAL+B2_COD == xFilial()+SG1->G1_COD
		If B2_LOCAL >= mv_par06 .Or. B2_LOCAL <= mv_par07
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Carrega array com dados do produto na data base.             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aSalAlm	:= CalcEst(SG1->G1_COD,B2_LOCAL,dDataBase+1)
			nSaldoEst+= aSalAlm[1]
		EndIf
		dbSkip()
	EndDo
	dbSelectArea("SG1")
	If lFirst
		@ li,000 PSAY __PrtThinLine()
		li++
	EndIf

	@ li,000    PSAY STR0011+SG1->G1_COD+" - "+SB1->B1_DESC+STR0012	//"PRODUTO PAI: "###"  SALDO ATUAL  ---> "
	@ li,PCol() PSAY nSaldoEst Picture PesqPictQt("B2_QATU",14) 
	
	_dEntrega:=CTOD('  /  /  ')
	dbSelectArea("SC2")
	dbSetOrder(6)
	dbSeek(xFilial()+SubStr(MV_PAR08,1,11)+SG1->G1_COD)
	While !Eof() .And. C2_FILIAL+C2_NUM+C2_ITEM+C2_SEQUEN+C2_PRODUTO == xFilial()+SubStr(MV_PAR08,1,11)+SG1->G1_COD
		//-- Valida se a OP deve ser Impressa ou n„o
		_dEntrega:=SC2->C2_DATPRF
		If !MtrAValOP(mv_par05, 'SC2')
			dbSkip()
			Loop
		EndIf
		If C2_LOCAL < mv_par06 .Or. C2_LOCAL > mv_par07
			dbSkip()
			Loop	
		EndIf		
		If Empty(C2_DATRF) .And. aSC2Sld() > 0
			nOPPro += aSC2Sld()
		EndIf
		dbSkip()
	EndDo
	
	If Empty(_dEntrega)
       @ li,PCol() PSAY "      PRODUTO SEM ORDEM DE PRODUÇÃO "
    Else       
	   @ li,PCol() PSAY "      O.P..:"+MV_PAR08+" Quantidade:"+	Transform(nOPPro,"@E 999,999.99")+"  DT. Entrega: "+DTOC(_dEntrega)
    EndIf	   
	
	nOPPro:=0		
	li++
	li++
	lFirst:=.T.
	
	For i:=1 to Len(aEstru)
		If mv_par03 == 2
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Posiciona SB1 para produto Pai	     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SB1")
			dbSetOrder(1)
			dbSeek(xFilial()+SubStr(aEstru[i],5,15))
			If B1_FANTASM == "S"
				Loop
			Endif
		Endif
		Imprime(titulo,tamanho,i,.F.)
	Next i
	
	ImpTot(titulo,cabec1,cabec2,nomeprog,Tamanho,nTipo)
	dbSelectArea("SG1")
	dbSetOrder(1)
	dbGoto(nReg)
	dbSkip()
	
Enddo

dbSelectArea("SC1")
dbSetOrder(1)
dbSelectArea("SC2")
dbSetOrder(1)
dbSelectArea("SC6")
dbSetOrder(1)
dbSelectArea("SC7")
dbSetOrder(1)

If li != 80
	Roda(nCntImpr,cRodaTxt,Tamanho)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea(cString)
Set Filter To
dbSetOrder(1)

If aReturn[5] == 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

Return NIL

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ Explode  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 03/08/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Faz a explosao de uma estrutura                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ Explode(ExpC1) 											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo do produto a ser explodido                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR875                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
STATIC Function Explode(cProduto)
LOCAL nRegi

dbSelectArea("SG1")
dbSetOrder(1)
dbSeek(xFilial()+cProduto)

While !Eof() .And. SG1->G1_FILIAL+SG1->G1_COD == xFilial()+cProduto
	
	nRegi:=Recno()
//  Buscar grupo do produto para quebrar por Linha de Producao - Honda Lock    
    dbSelectArea("SB1")
    dbSetOrder(1)
    dbSeek(xFilial()+SG1->G1_COMP)	
//	
	nProcura:=aScan(aEstru,SB1->B1_GRUPO+SG1->G1_COMP)
	If nProcura  = 0
		AADD(aEstru,SB1->B1_GRUPO+SG1->G1_COMP)
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se existe sub-estrutura                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	_cG1_COMP:=SG1->G1_COMP
	dbSelectArea("SG1")
    dbSetOrder(1)
	nRecno:=Recno()
	If dbSeek(xFilial()+_cG1_COMP,.F.)
		Explode(SG1->G1_COD)
	Else
		dbGoto(nRecno)
		nProcura:=aScan(aEstru,SB1->B1_GRUPO+SG1->G1_COMP)
		If nProcura  = 0
			AADD(aEstru,SB1->B1_GRUPO+SG1->G1_COMP)
		EndIf
	Endif
	
	dbGoto(nRegi)
	dbSkip()
	
Enddo
//
aEstru := aSort(aEstru)
//
Return(aEstru)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ Imprime  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 03/08/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Imprime a Analise de Estoque de cada Nivel de uma estrutura³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ Imprime()		 										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR875                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
STATIC Function Imprime(titulo,tamanho,i,lRelease4)
LOCAL lSB3:=.F.      
LOCAL aSalAlm  := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Zerando os valores das variaveis utiliadas na impressao      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nSaldoEst:=nEmp:=nSCPro:=nPCPro:=nOPPro:=nPVPro:=nSaldo99:=nSaldoL:=0
dUsai := CtoD("  /  /  ")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona os indices dos arquivos consultados                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC1")
dbSetOrder(2)

dbSelectArea("SC2")
dbSetOrder(2)

dbSelectArea("SC6")
dbSetOrder(2)

dbSelectArea("SC7")
dbSetOrder(2)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona SB1 										         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB1")
dbSetOrder(1)         
If dbSeek(xFilial()+SubStr(aEstru[i],5,15)) .And. AllTrim(SB1->B1_TIPO)#"PI"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona SB2											     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB2")
	dbSetOrder(1)
	dbSeek(xFilial()+SubStr(aEstru[i],5,15))                  
	While B2_FILIAL+B2_COD == xFilial()+SubStr(aEstru[i],5,15)
		If B2_LOCAL >= mv_par06 .And. B2_LOCAL <= mv_par07
			nEmp+=(If(mv_par05==1,SB2->B2_QEMP,If(mv_par05==2,SB2->B2_QEMPPRE,SB2->B2_QEMP+SB2->B2_QEMPPRE))+SB2->B2_RESERVA)
			If SB2->B2_LOCAL=="99"
			   aSalAlm := CalcEst(B2_COD,SB2->B2_LOCAL,dDataBase+1)
			   nSaldo99+= aSalAlm[1]
			EndIf
			If !(SB2->B2_LOCAL$"01|02|03|98|99" )
			   aSalAlm := CalcEst(B2_COD,SB2->B2_LOCAL,dDataBase+1)
			   nSaldoL+= aSalAlm[1]
			EndIf
			aSalAlm := CalcEst(B2_COD,SB2->B2_LOCAL,dDataBase+1)
			nSaldoEst+= aSalAlm[1]
			If dUsai < B2_USAI
        		dUsai := B2_USAI
			EndIf
		EndIf
		dbSkip()
	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona-se no arquivo de Demandas para pegar dados         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB3")
	lSB3:=dbSeek(xFilial()+SubStr(aEstru[i],5,15))
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aglutina as Solicitacos de Compra sem pedido colocado        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC1")
	dbSeek(xFilial()+SubStr(aEstru[i],5,15))
	While !Eof() .And. C1_FILIAL+C1_PRODUTO == xFilial()+SubStr(aEstru[i],5,15)
		//-- Valida se a OP deve ser Impressa ou n„o
		If !MtrAValOP(mv_par05, 'SC1')
			dbSkip()
			Loop
		EndIf
		If C1_LOCAL < mv_par06 .Or. C1_LOCAL > mv_par07
			dbSkip()
			Loop	
		EndIf
		If C1_QUJE < C1_QUANT .And. Empty(C1_PEDIDO)
			nSCPro += (C1_QUANT - C1_QUJE)
		EndIf
		dbSkip()
	EndDo
    
    nEmp     :=0//ESPECIFICO HONDA LOCK
    _dEntrCP :=CtoD('  /  /  ')  
    _cLoteCTL:=""  
	dbSelectArea("SD4")
	dbSetOrder(1)
	dbSeek(cSeek := xFilial("SD4") + SubStr(aEstru[i],5,15))
	do While ! Eof() .And. SD4->(D4_FILIAL + D4_COD) == cSeek
		If SD4->D4_QUANT < 0
			nSCPro += ( SD4->D4_QUANT * -1 )
		Endif
        _cLoteCTL:=SD4->D4_LOTECTL
        If SubStr(SD4->D4_OP,1,6)==SubStr(MV_PAR08,1,6)
		   nEmp+=SD4->D4_QUANT//ESPECIFICO HONDA LOCK
	       
	       dbSelectArea("SC2")
	       dbSetOrder(1)
	       dbSeek(xFilial("SC2")+SD4->D4_OP)
	       If AllTrim(SC2->(C2_NUM+C2_ITEM+C2_SEQUEN))==AllTrim(SD4->D4_OP)
	          _dEntrCP:=SC2->C2_DATPRI-SB1->B1_DIAANT//Data de Inicio da OP - Dias de Antecedencia
           EndIf	          
        EndIf
	    
	    dbSelectArea("SD4")
		dbSkip()
	Enddo		

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aglutina as Ordens de Producao em aberto                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC2")
	dbSeek(xFilial()+SubStr(aEstru[i],5,15))
	While !Eof() .And. C2_FILIAL+C2_PRODUTO == xFilial()+SubStr(aEstru[i],5,15)
		//-- Valida se a OP deve ser Impressa ou n„o
		If !MtrAValOP(mv_par05, 'SC2')
			dbSkip()
			Loop
		EndIf
		If C2_LOCAL < mv_par06 .Or. C2_LOCAL > mv_par07
			dbSkip()
			Loop	
		EndIf		
		If Empty(C2_DATRF) .And. aSC2Sld() > 0
			nOPPro += aSC2Sld()
		EndIf
		dbSkip()
	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aglutina os Pedidos de Vendas ainda nao entregues            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC6")
	dbSeek(xFilial()+SubStr(aEstru[i],5,15))
	While !Eof() .And. C6_FILIAL+C6_PRODUTO == xFilial()+SubStr(aEstru[i],5,15)
		//-- Valida se a OP deve ser Impressa ou n„o
		If !MtrAValOP(mv_par05, 'SC6')
			dbSkip()
			Loop
		EndIf
		If C6_LOCAL < mv_par06 .Or. C6_LOCAL > mv_par07
			dbSkip()
			Loop	
		EndIf
		If ( C6_QTDVEN - ( C6_QTDEMP + C6_QTDENT ) ) > 0
			nPVPro += ( C6_QTDVEN - ( C6_QTDEMP + C6_QTDENT ) )
		EndIf
		dbSkip()
	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aglutina os Pedidos de Compra ainda nao entregues            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC7")
	dbSeek(xFilial()+SubStr(aEstru[i],5,15))
	While !Eof() .And. C7_FILIAL+C7_PRODUTO == xFilial()+SubStr(aEstru[i],5,15)
		//-- Valida se a OP deve ser Impressa ou n„o
		If !MtrAValOP(mv_par05, 'SC7')
			dbSkip()
			Loop
		EndIf
		If C7_LOCAL < mv_par06 .Or. C7_LOCAL > mv_par07
			dbSkip()
			Loop	
		EndIf
		If (C7_QUANT-C7_QUJE) > 0 .And. Empty(C7_RESIDUO)
			nPCPro += (C7_QUANT-C7_QUJE)
		EndIf
		dbSkip()
	EndDo

	If !lRelease4
		If li > 58
			Cabec(titulo,cabec1,cabec2,nomeprog,Tamanho,nTipo)
		EndIf             
                                         
        If _cGrupoAnt#SB1->B1_GRUPO
           If !Empty(AllTrim(_cGrupoAnt))
              li++
           EndIf
           dbSelectArea("SBM")
	       dbSetOrder(1)
	       dbSeek(xFilial("SBM") + SB1->B1_GRUPO)
	       @ li,000 PSAY "GRUPO: "+SBM->BM_GRUPO+"-"+SBM->BM_DESC
	       li++                                                  
	       @ li,000 PSAY Replicate("-",50)
	       li++
        EndIf
		@ li,000 PSAY SB1->B1_COD
		@ li,016 PSAY Substr(SB1->B1_DESC,1,30)
		@ li,047 PSAY SB1->B1_TIPO
		@ li,050 PSAY SB1->B1_GRUPO
		@ li,055 PSAY SB1->B1_UM
		@ li,058 PSAY nSaldoEst    	Picture PesqPictQt("B2_QATU",14)
		@ li,073 PSAY nEmp				Picture PesqPictQt("B2_QEMP",14)
		//@ li,088 PSAY nSCPro    		Picture PesqPictQt("C1_QUANT",14)
		//@ li,103 PSAY nPCPro    		Picture PesqPictQt("C7_QUANT",14)		
		@ li,088 PSAY nSaldo99  		Picture PesqPictQt("C1_QUANT",14)		
		@ li,103 PSAY nSaldoL   		Picture PesqPictQt("C7_QUANT",14)
		@ li,118 PSAY nOPPro    		Picture PesqPictQt("C2_QUANT",14)
		@ li,133 PSAY nPVPro    		Picture PesqPictQt("C6_QTDVEN",14)
		@ li,148 PSAY RetFldProd(SB1->B1_COD,"B1_EMIN") Picture PesqPictQt("B1_EMIN",12)
		@ li,161 PSAY RetFldProd(SB1->B1_COD,"B1_LE") 	 Picture PesqPictQt("B1_LE",12)
		@ li,174 PSAY RetFldProd(SB1->B1_COD,"B1_PE") Picture PesqPictQt("B1_PE",12)
		@ li,180 PSAY RetFldProd(SB1->B1_COD,"B1_TIPE")
		If lSB3
			If SB3->B3_MEDIA != 0
				@ li,183 PSAY nSaldoEst/SB3->B3_MEDIA Picture "99999"
			Else
				@ li,183 PSAY "99999"
			EndIf
			//@ li,189 PSAY SB3->B3_MEDIA Picture PesqPictQt("B3_MEDIA",13)
		EndIf                                     
		@ li,192 PSAY _cLoteCTL
		@ li,204 PSAY DtoC(_dEntrCP)//SB2->B2_USAI
		If lSB3
			@ li,215 PSAY SB3->B3_CLASSE
		EndIf
		If SB1->B1_FANTASM == "S"
			@ li,218 PSAY "F"
		Else
			@ li,218 PSAY "N"
		Endif
		li++
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma no Totalizador por Produto Pai				       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nTotSaldo+=nSaldoEst
		nTotEmp+=nEmp
		nTotSCPro+=nSCPro
		nTotPCPro+=nPCPro
		nTotOPPro+=nOPPro
		nTotPVPro+=nPVPro
	EndIf
	_cGrupoAnt:=SB1->B1_GRUPO//Honda Lock
EndIf
Return
/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ ImpTot   ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 03/08/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Imprime Totais por Produto Pai                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ ImpTot()	    	 										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR875                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
STATIC Function ImpTot(titulo,cabec1,cabec2,nomeprog,Tamanho,nTipo)

li++
If li > 58
	Cabec(titulo,cabec1,cabec2,nomeprog,Tamanho,nTipo)
	li++
EndIf
@ li,000 PSAY STR0013	//"T O T A I S  :"
@ li,058 PSAY nTotSaldo			Picture PesqPictQt("B2_QATU",14)
@ li,073 PSAY nTotEmp			Picture PesqPictQt("B2_QEMP",14)
@ li,088 PSAY nTotSCPro    		Picture PesqPictQt("C1_QUANT",14)
@ li,103 PSAY nTotPCPro    		Picture PesqPictQt("C7_QUANT",14)
@ li,118 PSAY nTotOPPro    		Picture PesqPictQt("C2_QUANT",14)
@ li,133 PSAY nTotPVPro    		Picture PesqPictQt("C6_QTDVEN",14)
li++

Return
